10 REM Simple Bench Index: SCRUSS 202012 / Atari version: Faicuai (2021)
20 VF=59.92:DIM GR$(17):GR$="1.618033988749895":GOTO 80
30 REM SUBS - PLS step around
40 RETURN 
50 REM Output routine
60 X=R(B):GOSUB 250:FNP1=FNP:X=K(B)/R(B):GOSUB 250:X=(100*60/R(B)):GOSUB 220
65 PRINT FNP1;"s; ";FNP;"/s; I=";FNR
70 RETURN 
80 PRINT 
90 PRINT CHR$(125);:PRINT "BASIC BENCH INDEX"
100 PRINT ">I GOOD. NTSC C64=100"
120 PRINT 
130 REM Loop counts for TEST
140 DIM K(8)
150 REM 
160 DIM R(8)
170 FOR I=1 TO 8
180   READ X:K(I)=X
190 NEXT I:GOTO 280
200 REM MOD-10 function
210 FNM=2+X-INT(X/10)*10:RETURN 
220 REM ROUND function
230 FNR=INT(0.5+X):RETURN 
240 REM ONE decimal place function
250 X=X*10:GOSUB 230:FNP=FNR/10:RETURN 
260 REM Machine-specific TIME function
270 FND=(PEEK(20)+PEEK(19)*256)/VF:RETURN 
280 REM Benchmark Counter
290 LET B=1
300 REM Test STRING
310 DIM Q$(37):LET Q$="ABCDEFGHIJKLMNOPQRSTUVWXYZ1234567890 "
320 REM Test Array
330 DIM S(12)
340 FOR I=1 TO 12
350   LET S(I)=I
360 NEXT I
370 REM Scratch Variables
380 LET X=1
390 LET Y=1
400 REM *** 1: FOR test
410 PRINT "1/8 - FOR    : ";
420 POKE 19,0:POKE 20,0
430 FOR I=1 TO K(B)
440 NEXT I
450 GOSUB 260:LET R(B)=FND
460 GOSUB 60
470 REM *** 2: GOTO test
480 LET B=2
490 PRINT "2/8 - GOTO   : ";
500 POKE 19,0:POKE 20,0
510 FOR I=1 TO K(B)
520   GOTO 530
530 NEXT I
540 GOSUB 260:LET R(B)=FND
550 GOSUB 60
560 REM *** 3: GOSUB test
570 LET B=3
580 PRINT "3/8 - GOSUB  : ";
590 POKE 19,0:POKE 20,0
600 FOR I=1 TO K(B)
610   GOSUB 40
620 NEXT I
630 GOSUB 260:LET R(B)=FND
640 GOSUB 60
650 REM *** 4: IF test
660 LET B=4
670 PRINT "4/8 - IF     : ";
680 POKE 19,0:POKE 20,0
690 FOR I=1 TO K(B)
700   IF I>0 THEN NEXT I
710 GOSUB 260:LET R(B)=FND
720 GOSUB 60
730 REM *** 5: FN test
740 LET B=5
750 PRINT "5/8 - FN     : ";
760 POKE 19,0:POKE 20,0
770 FOR I=1 TO K(B)
780   X=I:GOSUB 200:X=FNM
790 NEXT I
800 GOSUB 260:LET R(B)=FND
810 GOSUB 60
820 REM *** 6: MATHS test
830 LET B=6
840 PRINT "6/8 - MATHS  : ";
850 POKE 19,0:POKE 20,0
860 FOR I=1 TO K(B)
870   LET Z=ATN(SQR(Y*Y)):Y=(SIN(Z)/COS(Z)+1)/Y
880 NEXT I
890 GOSUB 260:LET R(B)=FND:Z=Y
900 GOSUB 60
910 REM *** 7: STRING test
920 LET B=7
930 PRINT "7/8 - STRING : ";
940 POKE 19,0:POKE 20,0
950 FOR I=1 TO K(B)
960   Q$(37)=Q$(1):Q$(1,36)=Q$(2,37)
970 NEXT I
980 GOSUB 260:LET R(B)=FND
990 GOSUB 60
1000 REM *** 8: ARRAY test
1010 LET B=8
1020 PRINT "8/8 - ARRAY  : ";
1030 POKE 19,0:POKE 20,0
1040 FOR I=1 TO K(B)
1050   X=I:GOSUB 200:X=FNM
1060   LET S(X)=(S(X-1)+S(X)+S(X+1))/3
1070 NEXT I
1080 GOSUB 260:LET R(B)=FND
1090 GOSUB 60
1100 PRINT 
1110 REM *** Overall INDEX
1120 LET Y=0
1130 FOR I=1 TO 8
1140   LET Y=Y+R(I)
1150 NEXT I
1160 X=(100*8*60/Y):GOSUB 220:PRINT "Overall INDEX: ";FNR
1162 PRINT "MATHS result : ";Z
1164 PRINT "MATHS target : ";GR$
1180 END 
1190 REM C64 std - DO NOT edit
1200 DATA 40468,26522,21046,14568
1210 DATA 3642,383,4933,1675
